plugins {
    id 'java'
    id 'distribution'
    id 'org.hidetake.ssh' version '2.10.1'
}

group 'cobra'
version '2.1'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

distributions {
    main {
        contents {
            into('pairing') {
                from 'pairing'
            }
            from 'pairing_based_execution.sh'
            into('config') {
                from 'config'
            }
            into('lib') {
                from jar
                from(configurations.runtimeClasspath)
            }
            from 'scripts'
        }
    }
}

remotes {
    master {
		//Create a gradle.properties and set the following properties: remoteDeployMasterHost, remoteDeployMasterUser,
		// remoteDeployMasterPrivateKeyFile
		if (project.hasProperty("remoteDeployMasterHost")) {
			host = remoteDeployMasterHost
		}
		if (project.hasProperty("remoteDeployMasterUser")) {
			user = remoteDeployMasterUser
		}
		if (project.hasProperty("remoteDeployMasterPrivateKeyFile")) {
			identity = file(remoteDeployMasterPrivateKeyFile)
		}
    }
}

ssh.settings {
    fileTransfer = 'scp'
}

task localDeploy(dependsOn: installDist) {
    doLast {
        int nServers = 4
        int nClients = 1

        def fileSeparator = System.getProperty("file.separator")
        def src = project.buildDir.name + fileSeparator + "install" + fileSeparator + project.name
        def workingDirectory = project.buildDir.name + fileSeparator + "local" + fileSeparator

        println ("Deploying project into ${workingDirectory}")

        for (i in 0..<nServers) {
            def target = workingDirectory + "rep${i}"
            copy {
                from src
                into target
            }
        }

        for (i in 0..<nClients) {
            def target = workingDirectory + "cli${i}"
            copy {
                from src
                into target
            }
        }
    }
}

task remoteDeploy(dependsOn: installDist) {
    doLast {
        def fileSeparator = System.getProperty("file.separator")
        def src = project.buildDir.name + fileSeparator + "install" + fileSeparator + project.name
        def target = '~/'
        ssh.run {
            session(remotes.master) {
                put from: src, into: target
            }
        }
    }
}

tasks.register("simpleLocalDeploy") {
    dependsOn("installDist")

    doLast {
        def buildDir = project.layout.buildDirectory.asFile.get()
        def src = "${buildDir}${File.separator}install${File.separator}${project.name}"
        def workingDirectory = "${buildDir}${File.separator}local${File.separator}"

        copy {
            from(src)
            into(workingDirectory)
        }
    }
}

dependencies {
    implementation fileTree('lib'){include '*.jar'}

    // https://mvnrepository.com/artifact/org.bouncycastle/bcpkix-jdk18on
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.77'

    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78'

    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    implementation 'commons-codec:commons-codec:1.15'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
    implementation 'ch.qos.logback:logback-core:1.4.12'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    implementation 'ch.qos.logback:logback-classic:1.4.12'

    // https://mvnrepository.com/artifact/io.netty/netty-all
    implementation 'io.netty:netty-all:4.1.115.Final'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation 'org.slf4j:slf4j-api:1.7.32'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.10.1'
}
